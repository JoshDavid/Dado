 GenerateReleaseNotes←{
     ⍝ ⍺ ←→ [Project Folder|Space]
     ⍝ ⍵ ←→ Version '0.0.0'
     ⍺←0
     c←GetConfigSpace ⍺
     p←c.ProjectFolder
     v←⍵
     rf←p,'ReleaseNotes/'
     _←1 ⎕MKDIR rf
     rd←'-'@(' '∘=)⍕3↑⎕TS
     ⍝ Package Notes:
     pn←p GetCommits''
     t←'# ',c.ProjectName,' Version ',v,' Release Notes ',rd
     b←'Release '∘≡¨8↑¨pn.Message
     k←'~'≠↑¨pn.Message
     k←k∧'Merge'∘≢¨5↑¨pn.Message
     m←(k∧~∨\b)/pn.Message
     i←(⍕¨1+⍳≢m),¨⊂'. '
     rn←(⊂t),i,¨m
     cn←nget rf,'PackageNotes.md'
     _←(rn,(⊂''),cn)nput rf,'PackageNotes.md'
     ⍝ Application Notes
     an←nget rf,'ApplicationNotes.md'
     nn←p GenerateApplicationReleaseNotes rn
     _←(nn,(⊂''),an)nput rf,'ApplicationNotes.md'
     0
 }
