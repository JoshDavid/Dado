 LoadPackage←{
     ⍝ ⍺ ←→ Load dependent packages
     ⍝ ⍵ ←→ Package Folder
     ⍝ ← ←→ List of packages loaded
     ⍺←1
     ~⎕NEXISTS ⍵:,⊂'Not found: ',⍵
     c←GetConfigSpace ⍵
     s←c.ProjectSpace
     9=⎕NC s:,⊂'Already loaded :',⍵  ⍝ Already loaded
     p←(-1+(⌽s)⍳'.')↓s               ⍝ Parent Space
     q←⍎p ⎕NS''                      ⍝ Ensure it exists
     ws←⍵,'/APLSource.dws'           ⍝ Workspace
     _←{6::0 ⋄ z←s q.⎕CY ws}0        ⍝ Copy in the namespace
     r←⍎s                            ⍝ Project Space
     r.AcreConfig←c                  ⍝ Note it
     ⍺=0:r                           ⍝ Do not load dependent.
     n←c.Open                        ⍝ Dependent names
     b←~∨/¨n∊¨⊂'/\'                  ⍝ No Company included
     k←¯1↓↑⎕NPARTS ⍵                 ⍝ Once
     k←b{⍺:⍵ ⋄ ¯1↓↑⎕NPARTS ⍵}¨⊂k     ⍝ Maybe twice
     d←k,¨'/',¨n                     ⍝ Dependent Packages
     0=≢d:,⊂'Loaded: ',⍵             ⍝ None
     (⊂'Loaded: ',⍵),⊃,/∇¨d          ⍝ Load 'em
 }
