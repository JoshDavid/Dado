 LoadPackage←{
     ⍝ ⍵ ←→ Package Folder
     ⍝ ← ←→ Project Space Reference
     ~⎕NEXISTS ⍵:444 ⎕SIGNAL'Package folder not found'
     c←GetConfigSpace ⍵
     s←c.ProjectSpace
     p←(-1+(⌽s)⍳'.')↓s               ⍝ Parent Space
     q←⍎p ⎕NS''                      ⍝ Ensure it exists
     ws←⍵,'/APLSource.dws'           ⍝ Workspace
     _←{6::0 ⋄ z←s q.⎕CY ws}0        ⍝ Copy in the namespace
     r←⍎s                            ⍝ Project Space
     r.AcreConfig←c                  ⍝ Note it
     r                               ⍝ Project space
 }
