 TestCircularDependencies←{
     ⍝ Dado API
     A←##.Main
     ⍝ Clean up and initialize
     tf←Reset 0
     ⍝ Create Project C, with dependency on Project A
     c←⍎'#.TestProjects.ProjectC'⎕NS''
     _←c.⎕FX'SumInC←{' '+/⍵' '}'
     _←A.CreateProject tf,'ProjectC #.TestProjects.ProjectC'
     _←c A.SetConfigParam'URL' 'Tests/ProjectC'
     _←c A.AddDependency'Tests/ProjectA' '0.0.0' 'ThisIsA' 1
     _←c A.Commit'Add config parameters'
     _←c A.CreateNewVersion''
     ⍝ Create Project B, with dependency on A and C
     b←⍎'#.TestProjects.ProjectB'⎕NS''
     _←b.⎕FX'MaxInB←{' '⌈/⍵' '}'
     _←A.CreateProject tf,'ProjectB #.TestProjects.ProjectB'
     _←b A.SetConfigParam'URL' 'Tests/ProjectB'
     _←b A.AddDependency'Tests/ProjectA' '0.0.0' 'ProjA' 1
     _←b A.AddDependency'Tests/ProjectC' '0.0.0' 'ProjC' 1
     _←b A.Commit'Add config parameters'
     _←b A.CreateNewVersion''
     ⍝ Create Project A, with B as dependency
     a←⍎'#.TestProjects.ProjectA'⎕NS''
     _←a.⎕FX'ProductInA←{' '×/⍵' '}'
     _←A.CreateProject tf,'ProjectA #.TestProjects.ProjectA'
     _←a A.SetConfigParam'URL' 'Tests/ProjectA'
     _←a A.AddDependency'Tests/ProjectB' '0.0.0' 'ThisIsB' 1
     _←a A.Commit'Add config parameters'
     _←a A.CreateNewVersion''
     ⍝ Build Project A
     _←a A.Build'0.0.0'
     ⍝ Open Application A
     s←⎕NS''
     _←s.⎕CY a.AcreConfig.DistFolder,'Application/ProjectA/ProjectA.dws'
     s←s.TestProjects
     ⍝ Test Structure
     9≠s.ProjectA.ThisIsB.MaxInB 6 9 2:1
     24≠s.ProjectB.ProjA.ProductInA 2 3 4:1
     10≠s.ProjectB.ProjC.SumInC 1 2 3 4:1
     20≠s.ProjectC.ThisIsA.ProductInA 4 5:1
     ⍝ Good!
     0⊣Reset 0
 }
