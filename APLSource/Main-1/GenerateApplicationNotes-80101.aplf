 GenerateApplicationNotes←{
     ⍝ ⍺ ←→ [Project Folder|Space]
     ⍝ ⍵ ←→ Primary Project Notes
     ⍝ ← ←→ Combined release notes
     ⍺←0
     c←GetConfigSpace ⍺
     p←c.ProjectFolder
     nf←c.ReleaseNotesFolder
     pn←nget nf,'PackageNotes.md'
     fn←nf,'ApplicationNotes.md'
     in←GetIncludeInApplicationNotes c
     0=≢in:pn nput fn
     c←GetConfigSpace¨in
     kf←c.PackageFolder               ⍝ Dependent Package Folders
     rf←kf,¨⊂'ReleaseNotes/'          ⍝ Release Note Folders
     dn←nget¨rf,¨⊂'PackageNotes.md'   ⍝ Dependent package notes
     an←(⊂pn),dn                      ⍝ All Notes
     b←~(≢an)↑1
     q←b ProcessPackageNotes¨an
     d n←↑¨,/¨↓⍉⊃q
     n←n[⍒d]
     n←↑,/n,¨⊂⊂''
     n nput fn
 }
