 RetrieveDependency←{
     ⍝ ⍺ ←→ [Current Project State Override]
     ⍝ ⍵ ←→ Dependency Space
     ⍝ ← ←→ Project or Package Config Space
     0=⍵.⎕NC'URL':⍵ DependencyError'No URL specfied'
     0=⍵.⎕NC'Version':⍵ DependencyError'No version specified'
     u←⍵.URL
     v←⍵.Version
     lp←⍵.LocalProjects
     jf←(lp.URL⍳⊂u)⊃(lp.ProjectFolder),⊂''
     ⍺∧jf≢'':⍵ GetConfigSpace jf
     kn←u PackageName v
     kf←PackageFQP kn
     ⎕NEXISTS kf:⍵ GetConfigSpace kf
     u←(jf≢'')⊃u('file://',jf)
     ⎕←'Retrieving ',kn,' from ',u,'...'
     sw←'--depth 1 --branch ',v
     q←{'"',⍵,'"'}
     l←1↓∊' ',¨'clone'sw u(q kf)
     r←RunProcess'git'l
     r.ExitCode≠0:⍵ DependencyError(1⊃2↑r.StdErr)
     c←⍵ GetConfigSpace kf
     _←DeleteGitItems c
     _←DeleteDevSpaceFolders c
     _←c.IsApplication DeleteDevFolders c
     c
 }
