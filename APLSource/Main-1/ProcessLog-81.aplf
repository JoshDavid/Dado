 ProcessLog←{
     ⍝ ⍺ ←→ [Project Folder|Space]
     ⍝ ⍵ ←→ Results of git log  (with --name-status)
     ⍝ ⍺ ←→ Project Space
     ⍺←0
     p←GetProjectFolder ⍺
     l←⍵~⊂''
     b←'*'=↑¨l
     cl←b/l
     fl←DecodeOctalEncoding¨(~b)/l
     n←{¯1+-2-/⍸⍵,1}b         ⍝ Lines/Files per commit
     cc←≢cl
     ch un cd←↓⍉⊃{(','≠⍵)⊆⍵}¨cl
     ts←↓(cc,6)⍴1⊃' -:'⎕VFI,20↑[1]⊃cd
     q←{(' '≠⍵)⊆⍵}¨fl
     fs←0⊃¨q
     rn←'R'=↑¨fs
     fn1←1⊃¨q
     fn2←2⊃¨q,¨⊂⊂''
     s←⎕NS''
     s.OK←{(2=⍵)∨rn∧3=⍵}≢¨q ⍝ If not, probably due to blanks in file name
     s.IsAPLItem←'APLSource/'∘≡¨10↑¨fn1
     s.FileName1←fn1
     s.FileName2←fn2
     s.APLItemName1←s.IsAPLItem\p FileNamesToItemNames s.IsAPLItem/fn1
     s.APLItemName2←s.IsAPLItem\p FileNamesToItemNames s.IsAPLItem/fn2
     s.Status←↑¨fs
     s.Deleted←'D'=s.Status
     s.Renamed←'R'=s.Status
     s.CommitHash←n/1↓¨ch
     s.CommitBy←n/un
     s.CommitDate←n/cd
     s.CommitTS←n/ts
     s.FileName←,1↑s.Renamed⊖⊃fn1 fn2
     s.APLItemName←,1↑s.Renamed⊖⊃s.APLItemName1 s.APLItemName2
     s.Exists←0<⎕NC s.APLItemName
     s.MostRecent←s.IsAPLItem∧≠s.APLItemName
     s
 }
