 BuildInProcess←{
     ⍝ ⍺ ←→ Project Folder
     ⍝ ⍵ ←→ Version
     ⍝ ← ←→ Return code, error message
     ⍺←0
     pf←GetProjectFolder ⍺
     c←GetConfigSpace pf
     0=≢pf:1 'Not in a project'
     _←3 ⎕NDELETE pf,'dist'
     746::1 ⎕DMX.EM
     m←pf Retrieve ⍵
     _←c CopyProject¨m
     _←c GenerateApplicationNotes m
     _←c GenerateReleaseNotes 0
     f←m.ProjectFolder
     af←c.ApplicationFolder
     load←{⎕SE.UCMD']acre.OpenProject ',⍵,' ',⍺,' -track=off -dependencies=off'}
     s←⌽⍎¨(⌽m._ProjectSpace)load¨⌽f
     _←DeleteDevSpaces¨m
     _←m.⎕EX⊂'ProjectFolder'
     _←m.⎕FX⊂⎕NR'ProjectFolder'
     s.AcreConfig←s.⎕NS m
     _←s∘SetReferences¨s
     p←↑s
     #.⎕LX←SetQuadLX p
     _←0 ⎕SAVE af,c._Name,'.dws'

     _←m.⎕EX⊂'ProjectFolder'
     _←m.ProjectFolder←f
     s.AcreConfig←s.⎕NS m
     p.AcreConfig.ProjectFolder←pf
     p.AcreConfig.ApplicationFolder←pf,'dist/Application/',c._Name,'/'
     _←CopyUserCommandFile p
     rf←pf,'dist/ReleaseArtifacts/'
     _←3 ⎕MKDIR rf

     ⍝ Move this, can't work from here!
     _←p Execute'OnBuild'

     zs←pf,'dist/Application'
     zt←rf,c._Name,'-Application-',⍵,'.zip'
     _←Zip zs zt
     0 'OK'
 }
