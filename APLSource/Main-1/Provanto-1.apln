:Namespace Provanto
⍝ URL: https://github.com/the-carlisle-group/Provanto
⍝ Version: 0.0.3
⍝ Build date: March 1, 2023
⎕IO ⎕ML←0 1
 Assert←{
     0⊣'TEST FAILED'⎕SIGNAL 789/⍨~⍵
 }
 Coverage←{
     ~⍵.Profile:¯1 ''
     s←Spaces⍕⍵.CodeSpace
     s,←Spaces⍕⍵.TestSpace
     n←s.⎕NL⊂-3 4
     p←'.',¨⍨⍕¨s
     af←⊃,/p{⍺∘,¨⍵}¨n
     d←⎕PROFILE'data'
     b←d[;1]∊⊂⍬
     ef←b/d[;0]
     el←(+\b)⊆d[;1]
     nr←(⎕NR¨af)~¨⊂⊂' }'
     al←⍳¨≢¨nr
     ul←al~¨(el,⊂⍬)[ef⍳af]
     c←100×1-÷/≢¨∊¨ul al
     k←0<≢¨ul
     uf←~af∊ef
     (uf/ul)←⊂⍬
     z←k/af{0=≢⍵:⍺ ⋄ ⍺,'[',(⍕⍵),']'}¨ul  ⍝
     c z
 }
 DisplayCodeCoverage←{
     ⍵.Quiet>1:0
     ~⍵.Profile:0
     ⎕←'Code coverage: ',,'Q<%>I4'⎕FMT ⍵.Coverage
     ⍵.Coverage=100:0
     ⎕←'Untested code:'
     ⎕←↑{25>≢⍵:⍵ ⋄ (10↑⍵),⊂'... and ',(⍕¯10+≢⍵),' more.'}⍵.Untested
     0
 }
 DisplaySuite←{
     ⍺.Quiet>1:0
     r←⍵.Result
     h←'Number of tests:'(≢r)
     ⎕←'*'⍪(⍕h⍪⍺.Status,⍪+⌿r∘.=⍳5)⍪'*'
     0
 }
 DisplayTotal←{
     ⍵.Quiet>1:0
     n←≢⍵.Suites
     n=1:0
     r←⊃,/⍵.Results.Result
     h←('Total number of tests in ',(⍕n),' suites:')(≢r)
     ⎕←'*'⍪(⍕h⍪⍵.Status,⍪+⌿r∘.=⍳5)⍪'*'
     0
 }
 Execute←{
     s f←⍵
     0=⍺.⎕NC f:0
     (⍺⍎f),' s'
 }
 Run←{
     ⍝ ⍺ ←→ [Stop 0|1 [Quiet 0|1|2]]
     ⍝ ⍵ ←→ Test space, [Code space]
     ⍝ ← ←→ Result space
     ⍺←0
     z←⎕NS''
     z.(Stop Quiet)←2↑⍺
     z.(TestSpace CodeSpace)←2↑⍵,0
     z.(Status DecoratedStatus)←Status''
     z.Suites←{⍵↓⍨1<≢⍵}Spaces⍕z.TestSpace
     z.IsSuite←z.TestSpace≠⊃z.Suites
     z.Profile←z.CodeSpace≠0
     p←⎕PROFILE⍣z.Profile
     e←z.TestSpace∘Execute⍣z.IsSuite
     _←e z'Startup'
     _←p¨'Clear'('Start' 'coverage')
     z.Results←z RunSuite¨z.Suites
     _←p'stop'
     _←e z'Teardown'
     z.(Coverage Untested)←Coverage z
     _←p'clear'
     _←DisplayTotal z
     1:z←z⊣DisplayCodeCoverage z
 }
 RunSuite←{
     _←⍵ Execute ⍺'Startup'
     ⎕←'Running suite: ',⍕⍵
     _←⍵.⎕FX¨⎕NR¨'Assert' 'Try'
     r←⎕NS''
     f←⍵.⎕NL ¯3
     r.Function←f/⍨'Test'∘≡¨4↑¨f
     p←r.Function{⍵,'.',⍺}¨⊂⍕⍵
     r.Result←⍺ RunTest¨p
     _←⍵ Execute ⍺'Teardown'
     r⊣⍺ DisplaySuite r
 }
 RunTest←{
     r←⍺{
         0/⍨~⍺.Stop::1+789≠⎕DMX.EN
         ⍎⍵,' 0'
     }⍵
     ⍺.Quiet>0:r
     r⊣⎕←(r⊃⍺.DecoratedStatus),' ',⍵
 }
 Spaces←{
     ⍵≢⍕⍎⍵:⍬
     s←⍵∘,¨'.',¨(⍎⍵).⎕NL ¯9
     0=≢s:,⍎⍵
     ∊(⍎⍵),∇¨s
 }
 Status←{
     s←'Passed' 'Failed' 'Broken' 'N/A' 'Disabled'
     d←↓(↑':',¨⍨s),' ',↑3/¨' !!--'
     s d
 }
 Try←{
     ⍺←⊢
     0::⎕DMX.EN
     0⊣⍺ ⍺⍺ ⍵
 }
:EndNamespace
