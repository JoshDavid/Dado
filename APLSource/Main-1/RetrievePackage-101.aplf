 RetrievePackage←{
     ⍝ ⍺ ←→ URL
     ⍝ ⍵ ←→ Version, [Current Project State Override]

     ⍝ need to know where to look for projects
     jn u←⍺
     v b←2↑0,⍨⊆⍵
     ⍝ jn ResolveVersion v
     kn←jn PackageName v
     jf←ProjectFQP jn
     kf←PackageFQP kn
     je ke←⎕NEXISTS¨jf kf
     b∧je:jn
     ke:kn
     u←u ProjectURL jf
     ⎕←'Retrieving ',kn,' from ',u,'...'
     sw←'--depth 1 --branch ',v
     l←1↓∊' ',¨'clone'sw u kf
     r←RunProcess'git'l
     ⍝ c←GetConfigSpace kf
     ⍝ s←c.ProjectSpace,'_','_'@('.'∘=)v
     ⍝ _←kf SetConfigParam'ProjectSpace's
     kn
 }
