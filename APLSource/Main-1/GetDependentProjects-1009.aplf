 GetDependentProjects←{
     ⍝ ⍺ ←→ All Projects
     ⍝ ⍵ ←→ Project
     ⍝ ←→ Dependencies, Aliases, Inject
     z←⍬ ⍬ ⍬
     c←⍵.AcreConfig
     0=c.⎕NC'ZDependencies':z
     0=≢c.ZDependencies:z
     m←c.ZDependencies
     n←m.Name
     pj←c.ProjectAuthor∘{'/'∊⍵:⍵ ⋄ ⍺,'/',⍵}¨n ⍝ Do this somewhere else!
     pk←pj,¨'-',¨m.Version
     pn←⍺.AcreConfig.FullProjectName
     d←⍺[(pn⍳pk)⌊pn⍳pj]
     ⍝ a←d.AcreConfig.ProjectName
     a←m.Alias
     t←m.Inject
     0=c.⎕NC'AcreTools':d a t
     d←d,⍵.##.##.CarlisleGroup.AcreTools
     a←a,⊂c.AcreTools.Alias
     t←t,c.AcreTools.Inject
     d a t
 }
