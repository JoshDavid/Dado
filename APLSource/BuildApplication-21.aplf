 BuildApplication←{
     ⍝ ⍺ ←→ [Project Folder|Space]
     ⍝ ⍵ ←→ Undefined
     ⍝ Assumes all packages already created
     ⍺←0
     pc←GetConfigSpace ⍺                ⍝ Primary config space
     pf←pc.ProjectFolder
     af←pc.ApplicationFolder            ⍝ Application Folder
     _←2 ⎕MKDIR af                      ⍝ Create it
     dp←GetDependencies pf              ⍝ Dependent projects
     c←{0=≢dp:'' ⋄ GetConfigSpace¨dp}0  ⍝ Dependent Config spaces
     ac←pc,c                            ⍝ All config spaces
     sf←ac.PackageFolder                ⍝ Source package folders  - These could really come from anywhere..
     tf←af∘,¨ac.FullProjectName         ⍝ Target package folders, under application folder
     _←2 ⎕MKDIR¨tf                      ⍝ Create target dirs
     _←tf{~⎕NEXISTS ⍵:0 ⋄ ⍺ ⎕NCOPY ⍵}¨sf,¨⊂'Assets'  ⍝ Copy Assets
     _←tf ⎕NCOPY¨sf,¨⊂'acre.config'     ⍝ Copy config file
     _←tf ⎕NCOPY¨sf,¨⊂'ReleaseNotes'    ⍝ Copy release notes Folder
     _←BuildAppWS pc.ProjectFolder
     _←pc CopyAcreToolsPackage af
     _←⍺ CopyUserCommandFile 0
     rf←pf,'dist/ReleaseArtifacts/'
     _←3 ⎕MKDIR rf
     zs←pf,'dist/Application'
     zt←rf,pc.ProjectName,'-Application-',pc.Version,'.zip'
     _←Zip zs zt
     0
 }
