 BuildApplication←{
     ⍝ ⍵ ←→ Project Folder or Space
     ⍝ Assumes all packages already created
     pf←GetProjectFolder ⍵
     pc←GetConfigSpace pf               ⍝ Primary config space
     af←pc.ApplicationFolder            ⍝ Application Folder
     _←DeleteFolder ¯1↓af               ⍝ Delete it first
     _←2 ⎕MKDIR af                      ⍝ Create it
     dp←GetDependencies pf              ⍝ Dependent projects
     c←{0=≢dp:'' ⋄ GetConfigSpace¨dp}0  ⍝ Dependent Config spaces
     ac←pc,c                            ⍝ All config spaces
     sf←ac.PackageFolder                ⍝ Source package folders  - These could really come from anywhere..
     tf←af∘,¨ac.FullProjectName         ⍝ Target package folders, under application folder
     _←2 ⎕MKDIR¨tf                      ⍝ Create target dirs
     _←tf{~⎕NEXISTS ⍵:0 ⋄ ⍺ ⎕NCOPY ⍵}¨sf,¨⊂'Assets'  ⍝ Copy Assets
     _←tf ⎕NCOPY¨sf,¨⊂'acreconfig.txt'  ⍝ Copy config file
     RunInProcess'#.CarlisleGroup.AcreTools.BuildApplicationWorkspace'pc.ProjectFolder
 }
