 NewVersionBranch←{
     ⍝ ⍵ ←→ '' [base]      ←→ Patch Current Release
     ⍝ ⍵ ←→ 'x.y.0' [base] ←→ New Version to create
     ⍝ ⍵ ←→ 'x.y.z'        ←→ Existing version to patch
     IsDirty'':'Uncommited changes.'
     q m←2↑(⊆⍵),⊂'master'
     t←GetTags''
     c←q≡''
     v←c⊃q t.NextPatch
     n←GetNumericVersion v
     e←(⊂v)∊t.Tag
     (~e)∧(~c)∧0≠2⊃n:'New release must have 0 patch number.'
     ⍝ (~e)∧(~c)∧         :'New release must be greater than...'
     r←'.'@(' '∘=)⍕n+0 0 e
     (⊂r)∊t.Tag:'Target release ',r,' already exists.'
     ⎕←⊃PullMaster''
     ⎕←⊃RunGitCommand'checkout -b version/',r,' ',e⊃m v
     RefreshProject 0
 }
