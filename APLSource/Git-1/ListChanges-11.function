 ListChanges←{
     fqdn←{0=≢⍵:⍵ ⋄ 'itemname'⎕SE.acre.run ⍵}
     ref←⍵
     settings←⍵.AcreConfig
     branch←GetCurrentBranch ref
     repName←settings.ProjectFolder
     deleted←GetDeletedFiles ref
     untracked←GetUntrackedFiles ref
     untracked~←deleted
     untracked←(~(5↑¨untracked)∊⊂'.acre')/untracked

     changed←⎕CMD'git -C "',settings.ProjectFolder,'"  diff --name-only'
     data←⍬
     data,←(⊂'Changed: '),¨fqdn changed
     data,←(⊂'Added: '),¨fqdn untracked
     data,←(⊂'Deleted: '),¨fqdn deleted
     ⎕←''
     ⎕←(20↑'Repository: '),repName
     ⎕←(20↑'Branch: '),branch
     ⎕←'Not Committed!'
     ⎕←''
     ⎕←⊃data
 }
