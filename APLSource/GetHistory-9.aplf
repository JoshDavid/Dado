 GetHistory←{
     ⍝ ⍺ ←→ [Project Folder|Space]
     ⍝ ⍵←'' for entire commit history
     ⍝  ←'filename' for specific file
     ⍝  ←'itemname' #.Space.Foo, etc
     ⍺←0
     cp←GetProjectFolder ⍺            ⍝ context or calling project folder
     q←'#'=↑⍵                         ⍝ Fully qualified name.
     ⍝ Use cs
     n←GetFullyQualifiedItemName ⍵
     p←{
         q:(GetProjectSpace ExistingAncestor n).ProjectFolder
         cp
     }0
     f←p GetFileName n
     g←'log  --name-status --pretty="*%h,%an,%ai"'
     r←p RunGitCommand g,(f≢'')/' --follow -- ',f
     s←p ProcessLog r
     f≡'':s
     h fn←s.CommitHash s.FileName
     s.Diff←h{7↓p RunGitCommand'show ',⍺,' ',⍵}¨fn
     s.Value←h{p RunGitCommand'show ',⍺,':',⍵}¨fn
     s
 }
