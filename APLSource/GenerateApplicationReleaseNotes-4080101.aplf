 GenerateApplicationReleaseNotes←{
     ⍝ ⍺ ←→ Project Folder or Space
     ⍝ ⍵ ←→ Primary Project Notes
     ⍝ ← ←→ Combined release notes
     ⍺←0
     p←GetProjectSpace ⍺
     pf←GetProjectFolder p
     df←GetDependencies pf            ⍝ Primary,Dependent projects
     0=≢df:⍵
     c←GetConfigSpace¨df
     kf←c.PackageFolder               ⍝ Dependent Package Folders
     rf←kf,¨⊂'ReleaseNotes/'          ⍝ Release Note Folders
     pn←nget¨rf,¨⊂'PackageNotes.md'   ⍝ Dependent package notes
     ln←{(⍵⍳⊂'')↑⍵}¨pn                ⍝ Keep only lastest notes
     ln←{z←⍵ ⋄ z[0]←⊂'#',0⊃⍵ ⋄ z}¨ln  ⍝ Add heading level for dependent packages
     c←GetConfigSpace¨kf              ⍝ Get the config file for each package
     rd←c.BuildDate                   ⍝ Release date for each package in app
     pr←p GetLastReleaseDate''        ⍝ Previous Release Date of Primary package
     gt←{(⍳2)≢⍋⍺ ⍵}                   ⍝ TAO gt
     b←rd gt¨⊂pr                      ⍝ Keep these notes
     ¯1↓↑,/((⊂⍵),b/ln),¨⊂⊂''          ⍝ Application Notes
 }
