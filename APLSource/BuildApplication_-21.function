 BuildApplication_←{
     ⍝ ⍺ ←→ Project Space
     ⍝ Assumes all packages created, ready to go.
     pc←⍺.AcreConfig                         ⍝ Primary config space
     af←pc.ApplicationFolder                 ⍝ Application Folder
     _←DeleteFolder ¯1↓af                    ⍝ Delete it first
     _←2 ⎕MKDIR af                           ⍝ Create it
     s←⍎¨(⎕SE.UCMD']ACRE.Projects')[;0]      ⍝ All project/package spaces
     c←s.AcreConfig                          ⍝ All Config spaces
     _←2 ⎕MKDIR¨{af,'/',⍵}¨∪c.ProjectAuthor  ⍝ Create Company/Author Folders
     sf←¯1↓¨c.PackageFolder                  ⍝ Source folders
     tf←af∘,¨c.FullProjectName               ⍝ Target folders
     _←CopyFolder¨sf{⍺ ⍵}¨tf                 ⍝ Copy all packages to application folder
     _←RunInProcess'#.CarlisleGroup.AcreTools.BuildApplicationWS_'⍺.AcreConfig.ProjectFolder
     0                                             ⍝ Exit process
 }
